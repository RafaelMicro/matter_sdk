# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/pigweed.gni")
import("//build_overrides/rt582_sdk.gni")

import("${build_root}/config/defaults.gni")
import("${rt582_sdk_build_root}/rt582_executable.gni")
import("${rt582_sdk_build_root}/rt582_sdk.gni")

import("${chip_root}/examples/common/pigweed/pigweed_rpcs.gni")
import("${chip_root}/src/platform/device.gni")

if (chip_enable_pw_rpc) {
  import("//build_overrides/pigweed.gni")
  import("$dir_pw_build/target_types.gni")
}

assert(current_os == "freertos")

rt582_project_dir = "${chip_root}/examples/light-switch-app/rt582"
examples_plat_dir = "${chip_root}/examples/platform/rt582"
# _freertos_root = "${chip_root}/third_party/rafael/sdk/Portable/freertos/repo"

declare_args() {
  # Dump memory usage at link time.
  chip_print_memory_usage = false

  # PIN code for PASE session establishment.
  setupPinCode = 20202021
  setupDiscriminator = 3840

  # Monitor & log memory usage at runtime.
  enable_heap_monitoring = false

  # Enable Sleepy end device
  enable_sleepy_device = false

  # OTA timeout in seconds
  OTA_periodic_query_timeout = 86400

  # Disable LCD on supported devices
  disable_lcd = true
}

declare_args() {
  # Enables LCD Qr Code on supported devices
  show_qr_code = !disable_lcd
}

# qr code cannot be true if lcd is disabled
assert(!(disable_lcd && show_qr_code))

# Sanity check
assert(!(chip_enable_wifi && chip_enable_openthread))

rt582_sdk("sdk") {

  sources = [
    "${rt582_project_dir}/include/CHIPProjectConfig.h",
    "${examples_plat_dir}/config/FreeRTOSConfig.h",
  ]

  include_dirs = [
    "${chip_root}/src/platform/RT582",
    "${rt582_project_dir}/include",
    "${examples_plat_dir}",
    "${chip_root}/src/lib",
  ]

  defines = [
    "CHIP_DEVICE_CONFIG_USE_TEST_SETUP_PIN_CODE=${setupPinCode}",
    "CHIP_DEVICE_CONFIG_USE_TEST_SETUP_DISCRIMINATOR=${setupDiscriminator}",
    "OTA_PERIODIC_TIMEOUT=${OTA_periodic_query_timeout}",
  ]

  if (chip_build_platform_attestation_credentials_provider) {
    defines += [ 
      "RAFAEL_CERTS_ENABLED=1",
    ]
  }

  if (chip_enable_pw_rpc) {
    defines += [
      "HAL_VCOM_ENABLE=1",
      "PW_RPC_ENABLED",
    ]
  }
}

rt582_executable("light_switch_app") {
  output_name = "chip-rt582-light-switch-app-example.out"
  include_dirs = [ 
    "include",
    "${examples_plat_dir}/projcrt_peripheral/light_switch_app"
  ]
  defines = []

  sources = [
    "${examples_plat_dir}/BaseApplication.cpp",
    "${examples_plat_dir}/heap_4_rt582.c",
    "${examples_plat_dir}/init_rt582Platform.cpp",
    "${examples_plat_dir}/projcrt_peripheral/light_switch_app/init_light_switch_app_rt582Platform.cpp",
    "${examples_plat_dir}/matter_config.cpp",
    "${examples_plat_dir}/uart.cpp",
    "src/AppTask.cpp",
    "src/binding-handler.cpp",
    "src/ZclCallbacks.cpp",
    "src/main.cpp",
  ]

  deps = [
    ":sdk",
    "${chip_root}/examples/light-switch-app/light-switch-common",
    "${chip_root}/examples/providers:device_info_provider",
    "${chip_root}/src/lib",
    "${chip_root}/src/setup_payload", 
    "${chip_root}/third_party/rafael/sdk/Portable/freertos",
  ]

  # OpenThread Settings
  if (chip_enable_openthread) {
    deps += [
      "${chip_root}/third_party/openthread:openthread",
      "${chip_root}/third_party/openthread:openthread-platform",
      "${examples_plat_dir}:rt582-matter-shell",
    ]
  }

  deps += [
    "${chip_root}/src/lib/shell:shell_core",
    "${chip_root}/src/lib/shell",
  ]

  if (chip_enable_ota_requestor) {
    defines += [ "RT582_OTA_ENABLED=1" ]
    sources += [ "${examples_plat_dir}/OTAConfig.cpp" ]
  }

  if (chip_enable_pw_rpc) {
    defines += [
      "PW_RPC_ENABLED",
      "PW_RPC_ATTRIBUTE_SERVICE=1",
      "PW_RPC_BUTTON_SERVICE=1",
      "PW_RPC_DESCRIPTOR_SERVICE=1",
      "PW_RPC_DEVICE_SERVICE=1",
      "PW_RPC_LIGHTING_SERVICE=1",
      "PW_RPC_OTCLI_SERVICE=1",
      "PW_RPC_THREAD_SERVICE=1",
      "PW_RPC_TRACING_SERVICE=1",
    ]

    sources += [
      "${chip_root}/examples/common/pigweed/RpcService.cpp",
      "${chip_root}/examples/common/pigweed/rt582/PigweedLoggerMutex.cpp",
      "${examples_plat_dir}/PigweedLogger.cpp",
      "${examples_plat_dir}/Rpc.cpp",
    ]

    deps += [
      "$dir_pw_hdlc:rpc_channel_output",
      "$dir_pw_stream:sys_io_stream",
      "$dir_pw_trace",
      "$dir_pw_trace_tokenized",
      "$dir_pw_trace_tokenized:trace_rpc_service",
      "${chip_root}/config/rt582/lib/pw_rpc:pw_rpc",
      "${chip_root}/examples/common/pigweed:attributes_service.nanopb_rpc",
      "${chip_root}/examples/common/pigweed:button_service.nanopb_rpc",
      "${chip_root}/examples/common/pigweed:descriptor_service.nanopb_rpc",
      "${chip_root}/examples/common/pigweed:device_service.nanopb_rpc",
      "${chip_root}/examples/common/pigweed:lighting_service.nanopb_rpc",
      "${chip_root}/examples/common/pigweed:ot_cli_service.nanopb_rpc",
      "${chip_root}/examples/common/pigweed:thread_service.nanopb_rpc",
      "${examples_plat_dir}/pw_sys_io:pw_sys_io_rt582",
    ]

    deps += pw_build_LINK_DEPS

    include_dirs += [
      "${chip_root}/examples/common",
      "${chip_root}/examples/common/pigweed/rt582",
    ]
  }

  ldscript = "${examples_plat_dir}/ldscript/rt58x.ld"

  inputs = [ ldscript ]

  ldflags = [ "-T" + rebase_path(ldscript, root_build_dir) ]

  ldflags += [
    "-Wl,--print-memory-usage",
    "-fstack-usage",
    "-Wunused-variable"
  ]

  # Attestation Credentials
  if (chip_build_platform_attestation_credentials_provider) {
    deps += [ "${chip_root}/src/platform/RT582:rt582-attestation-credentials" ]
  }

  output_dir = root_out_dir
}

group("rt582") {
  deps = [ ":light_switch_app" ]
}

group("default") {
  deps = [ ":rt582" ]
}
