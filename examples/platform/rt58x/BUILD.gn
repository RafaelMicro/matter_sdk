# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/chip.gni")
import("//build_overrides/rt58x_sdk.gni")
import("${chip_root}/src/lib/lib.gni")
import("${chip_root}/src/platform/device.gni")
import("${rt58x_sdk_build_root}/rt58x_sdk.gni")

declare_args() {
  # Configuration file for openthread.
  openthread_config = "${chip_root}/examples/platform/rt58x/project_include"
}

config("chip_examples_project_config") {
  include_dirs = [ "project_include" ]

  # Link options that provide a replacement for dynamic memory operations in standard
  # library with the FreeRTOS malloc in platform code.
  ldflags = [
    # memory allocation -- these must be re-entrant and do locking
    "-Wl,--wrap=malloc",
    "-Wl,--wrap=free",
    "-Wl,--wrap=realloc",
    "-Wl,--wrap=calloc",
    "-Wl,--wrap=MemoryAlloc",

    # Wrap these in case internal newlib call them (e.g. strdup will)
    # directly call _malloc_r)
    "-Wl,--wrap=_malloc_r",
    "-Wl,--wrap=_realloc_r",
    "-Wl,--wrap=_free_r",
    "-Wl,--wrap=_calloc_r",
  ]
}

source_set("openthread_core_config_rt58x_chip_examples") {
  if (chip_enable_openthread) {
    sources = [ "project_include/OpenThreadConfig.h" ]

    public_deps = [
      "${chip_root}/third_party/openthread/platforms/rt58x/:openthread_core_config_rt58x",
      "${rt58x_sdk_build_root}:rt58x_sdk",
    ]

    public_configs = [ ":chip_examples_project_config" ]
  }
}

config("ICD-subscription-callback-config") {
  defines = [ "CHIP_CONFIG_USE_SUBSCRIPTION_CALLBACKS=1" ]
}

source_set("rt58x-subscription-callback") {
  sources = [
    "SubscriptionCallback.cpp",
    "SubscriptionCallback.h",
  ]

  public_deps = [
    "${chip_root}/src/app:app",
    "${rt58x_sdk_build_root}:rt58x_sdk",
  ]

  public_configs = [ ":ICD-subscription-callback-config" ]
}

source_set("rt58x-matter-shell") {
  if (chip_build_libshell) {
    defines = [ "ENABLE_CHIP_SHELL" ]

    sources = [ "matter_shell.cpp" ]
    include_dirs = [ "." ]

    public_deps = [
      "${chip_root}/examples/shell/shell_common:shell_common",
      "${chip_root}/src/lib/shell:shell",
      "${chip_root}/src/lib/shell:shell_core",
    ]
  }
}
