# Copyright (c) 2020 Project CHIP Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build_overrides/build.gni")
import("//build_overrides/chip.gni")
import("//build_overrides/pigweed.gni")
import("//build_overrides/rt58x_sdk.gni")

import("${build_root}/config/defaults.gni")
import("${chip_root}/src/platform/device.gni")
import("${rt58x_sdk_build_root}/rt58x_executable.gni")
import("${rt58x_sdk_build_root}/rt58x_sdk.gni")

assert(current_os == "freertos")

rt58x_project_dir = "${chip_root}/examples/thermostat/rt58x"
examples_plat_dir = "${chip_root}/examples/platform/rt58x"

# _freertos_root = "${chip_root}/third_party/rafael/sdk/Portable/freertos/repo"

declare_args() {
  # Dump memory usage at link time.
  chip_print_memory_usage = false

  # PIN code for PASE session establishment.
  setupPinCode = 20202021
  setupDiscriminator = 3840

  # Monitor & log memory usage at runtime.
  enable_heap_monitoring = false

  # Enable Sleepy end device
  enable_sleepy_device = false

  # OTA timeout in seconds
  OTA_periodic_query_timeout = 86400

  #using default certification declaration
  chip_using_default_cd = false

  # Disable LCD on supported devices
  disable_lcd = true
}

declare_args() {
  # Enables LCD Qr Code on supported devices
  show_qr_code = !disable_lcd
}

# qr code cannot be true if lcd is disabled
assert(!(disable_lcd && show_qr_code))

# Sanity check
assert(!(chip_enable_wifi && chip_enable_openthread))

rt58x_sdk("sdk") {
  sources = [
    "${rt58x_project_dir}/include/CHIPProjectConfig.h",
  ]

  include_dirs = [
    "${chip_root}/src/platform/RT58x",
    "${rt58x_project_dir}/include",
    "${examples_plat_dir}",
    "${chip_root}/src/lib",
  ]

  defines = [
    "CHIP_DEVICE_CONFIG_USE_TEST_SETUP_PIN_CODE=${setupPinCode}",
    "CHIP_DEVICE_CONFIG_USE_TEST_SETUP_DISCRIMINATOR=${setupDiscriminator}",
    "OTA_PERIODIC_TIMEOUT=${OTA_periodic_query_timeout}",
  ]

  if (chip_build_platform_attestation_credentials_provider) {
    defines += [ "RAFAEL_CERTS_ENABLED=1" ]
    if(chip_using_default_cd) {
      defines += [ "RAFAEL_CERTS_USE_EXAMPLE_CD=1" ]
    }
  }
}

rt58x_executable("thermostat") {
  output_name = "chip-rt58x-thermostat-example.out"
  include_dirs = [ "include" ]
  defines = []

  sources = [
    "${examples_plat_dir}/BaseApplication.cpp",
    "${examples_plat_dir}/heap_4_rt58x.c",
    "${examples_plat_dir}/init_rt58xPlatform.cpp",
    "${examples_plat_dir}/matter_config.cpp",
    "${examples_plat_dir}/uart.cpp",
    "src/AppTask.cpp",
    "src/SensorManager.cpp",
    "src/TemperatureManager.cpp",
    "src/ZclCallbacks.cpp",
    "src/init_device_environment.cpp",
    "src/main.cpp",
  ]
  if (chip_build_libshell) {
    sources += [ "${examples_plat_dir}/cmd_rafael.cpp" ]
  }
  deps = [
    ":sdk",
    "${chip_root}/examples/providers:device_info_provider",
    "${chip_root}/examples/thermostat/thermostat-common",
    "${chip_root}/src/lib",
    "${chip_root}/src/setup_payload",
    "${chip_root}/third_party/rafael/sdk/Portable/freertos",
  ]

  # OpenThread Settings
  if (chip_enable_openthread) {
    deps += [
      "${chip_root}/third_party/openthread:openthread",
      "${chip_root}/third_party/openthread:openthread-platform",
      "${examples_plat_dir}:rt58x-matter-shell",
    ]
  }

  if (!enable_sleepy_device) {
    deps += [ "${examples_plat_dir}:rt58x-subscription-callback" ]
  }

  deps += [
    "${chip_root}/src/lib/shell",
    "${chip_root}/src/lib/shell:shell_core",
  ]

  if (chip_enable_ota_requestor) {
    defines += [ "RT58x_OTA_ENABLED=1" ]
    sources += [ "${examples_plat_dir}/OTAConfig.cpp" ]
  }

  ldscript = "${examples_plat_dir}/ldscript/rt58x.ld"

  inputs = [ ldscript ]

  ldflags = [ "-T" + rebase_path(ldscript, root_build_dir) ]

  ldflags += [
    "-Wl,--print-memory-usage",
    "-fstack-usage",
    "-Wunused-variable",
  ]

  # Attestation Credentials
  if (chip_build_platform_attestation_credentials_provider) {
    deps += [ "${chip_root}/src/platform/RT58x:rt58x-attestation-credentials" ]
  }

  output_dir = root_out_dir
}

group("rt58x") {
  deps = [ ":thermostat" ]
}

group("default") {
  deps = [ ":rt58x" ]
}
